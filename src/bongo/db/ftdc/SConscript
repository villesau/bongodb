# -*- mode: python -*-
Import("env")

env = env.Clone()

ftdcEnv = env.Clone()
ftdcEnv.InjectThirdPartyIncludePaths(libraries=['zlib'])

ftdcEnv.Library(
    target='ftdc',
    source=[
        'block_compressor.cpp',
        'collector.cpp',
        'compressor.cpp',
        'controller.cpp',
        'decompressor.cpp',
        'file_manager.cpp',
        'file_reader.cpp',
        'file_writer.cpp',
        'util.cpp',
        'varint.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/bongo/base',
        '$BUILD_DIR/bongo/bson/util/bson_extract',
        '$BUILD_DIR/bongo/db/server_options_core',
        '$BUILD_DIR/bongo/db/service_context',
        '$BUILD_DIR/third_party/s2/s2', # For VarInt
        '$BUILD_DIR/third_party/shim_zlib',
    ],
)

platform_libs = []

if env.TargetOSIs('linux'):
    platform_libs = [
        '$BUILD_DIR/bongo/util/procparser'
    ]
elif env.TargetOSIs('windows'):
    platform_libs = [
        '$BUILD_DIR/bongo/util/perfctr_collect'
    ]

env.Library(
    target='ftdc_bongod',
    source=[
        'ftdc_commands.cpp',
        'ftdc_bongod.cpp',
        'ftdc_system_stats.cpp',
        'ftdc_system_stats_${TARGET_OS}.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/bongo/base',
        '$BUILD_DIR/bongo/db/commands',
        '$BUILD_DIR/bongo/db/repl/repl_coordinator_global',
        '$BUILD_DIR/bongo/db/server_parameters',
        '$BUILD_DIR/bongo/db/storage/storage_options',
        '$BUILD_DIR/bongo/util/processinfo',
        'ftdc'
    ] + platform_libs,
)

env.CppUnitTest(
    target='ftdc_test',
    source=[
        'compressor_test.cpp',
        'controller_test.cpp',
        'file_manager_test.cpp',
        'file_writer_test.cpp',
        'ftdc_test.cpp',
        'util_test.cpp',
        'varint_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/bongo/util/clock_source_mock',
        'ftdc',
    ],
)
